set(SndFile_VERSION @PROJECT_VERSION@)
set(SndFile_VERSION_MAJOR @PROJECT_VERSION_MAJOR@)
set(SndFile_VERSION_MINOR @PROJECT_VERSION_MINOR@)
set(SndFile_VERSION_PATCH @PROJECT_VERSION_PATCH@)

set (SndFile_WITH_EXTERNAL_LIBS @SndFile_WITH_EXTERNAL_LIBS@)
set (SndFile_WITH_MPEG @SndFile_WITH_MPEG@)
set (SndFile_STATIC NOT @BUILD_SHARED_LIBS@)

@PACKAGE_INIT@

include (CMakeFindDependencyMacro)

if ((SndFile_WITH_EXTERNAL_LIBS OR SndFile_WITH_MPEG) AND SndFile_STATIC)
    list(APPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR})
endif()

if (SndFile_WITH_EXTERNAL_LIBS AND SndFile_STATIC)
	find_dependency (Ogg 1.3)
	find_dependency (Vorbis)
	find_dependency (FLAC)
	find_dependency (Opus)
endif ()

if (SndFile_WITH_MPEG AND SndFile_STATIC)
	find_dependency (mp3lame)
	find_dependency (MPG123)
endif ()

include (${CMAKE_CURRENT_LIST_DIR}/SndFileTargets.cmake)

set_and_check (SndFile_INCLUDE_DIR "@PACKAGE_INCLUDE_INSTALL_DIR@")
set (SNDFILE_INCLUDE_DIR ${SndFile_INCLUDE_DIR})

set (SndFile_LIBRARY SndFile::sndfile)
set (SNDFILE_LIBRARY SndFile::sndfile)
set (SndFile_LIBRARIES SndFile::sndfile)
set (SNDFILE_LIBRARIES SndFile::sndfile)


check_required_components(SndFile)

set (SNDFILE_FOUND 1)
